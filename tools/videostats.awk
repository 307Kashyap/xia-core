#
# videostats.awk
# read a log file generated by the dashclient and print the associated stats
#
# awk -f videostats.awk file1 [file2 ...]

BEGIN {
	v = 0;		# # of videos processed

	# stats for current video
	b  = 0;		# total bytes
	t  = 0.0;	# total time (s)
	tp = 0.0;	# cumulative tput scores
	r  = 0;		# # of video chunks
	s  = 0;		# total # of sefgmetns in the video

	# stats for all videos
	tb  = 0;
	tt  = 0.0;
	ttp = 0.0;
	tr  = 0;
}

/starting/ {
	v += 1;		# bump # of videos seen
	name = $5;	# save the name of the current video
}

/num segments/ {
	s = $6;
}

/closing/ {
	if (r == 0 || s == 0) {
		printf("%s: FAILED\n", FILENAME);
	} else {
		if (r != s) {
			status = "INCOMPLETE";
		} else {
			status = "SUCCESS";
		}
		printf("%s: %-10s %d/%d segments %d bytes %1.3f seconds %1.3f mbps\n",
			FILENAME, status, r, s, b, t, tp / r);
	}

	# update the global stats
	tb  += b;
	tt  += t;
	ttp += tp;
	tr  += r;

	# reset video specific stats
	b = 0;
	t = 0.0;
	tp = 0.0;
	r = 0;
	s = 0;
}

/bytes/ {
	# update video specific stats
	r += 1;
	b  += $4;
	t  += $6;
	tp += $8;
}

END {
	# and finally show the cumulative stats
	print "totals";
	print tr, "segments";
	print tb / 1000000, "MB";
	print tt, "seconds";
	print ttp/ tr, "mbps";
}
