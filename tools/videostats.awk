#
# videostats.awk
# read a log file generated by the dashclient and print the associated stats
#
# awk -f videostats.awk file1 [file2 ...]

BEGIN {
	v = 0;		# # of videos processed

	# stats for current video
	b  = 0;		# total bytes
	t  = 0.0;	# total time (s)
	tp = 0.0;	# cumulative tput scores
	r  = 0;		# # of video chunks

	# stats for all videos
	tb  = 0;
	tt  = 0.0;
	ttp = 0.0;
	tr  = 0;
}

/starting/ {
	v += 1;		# bump # of videos seen
	name = $5;	# save the name of the current video
}

/closing/ {
	print FILENAME ":", name ":",  r, "segments", b, "bytes", t, "seconds", tp / r, "mbps";

	# update the global stats
	tb  += b;
	tt  += t;
	ttp += tp;
	tr  += r;

	# reset video specific stats
	b = 0;
	t = 0.0;
	tp = 0.0;
	r = 0;
}

/bytes/ {
	# update video specific stats
	r += 1;
	b  += $4;
	t  += $6;
	tp += $8;
}

END {
	# and finally show the cumulative stats
	print "totals";
	print tr, "segments";
	print tb / 1000000, "MB";
	print tt, "seconds";
	print ttp/ tr, "mbps";
}
