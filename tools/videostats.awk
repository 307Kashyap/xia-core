#
# videostats.awk
# read a log file generated by the dashclient and print the associated stats
#
# awk -f videostats.awk file1 [file2 ...]

BEGIN {
	v = 0;		# # of videos processed

	# stats for current video
	b  = 0;		# total bytes
	t  = 0.0;	# total time (s)
	tp = 0.0;	# cumulative tput scores
	r  = 0;		# # of video chunks
	s  = 0;		# total # of segments in the video

	vinc = 0;	# video quality incresase
	vdec = 0;	# video rate decrease
	vlast = 0; 	# last video segments bitrate

	# stats for all videos
	tb  = 0;
	tt  = 0.0;
	ttp = 0.0;
	tr  = 0;

	tvinc = 0;	# global video rate increases
	tvdec = 0;	# global video rate decreases
}

/starting/ {
	v += 1;		# bump # of videos seen
	name = $5;	# save the name of the current video
}

/num segments/ {
	s = $6;
}

/setting bitrate/ {

	rate = $7;
	if (rate > vlast) {
		vinc++;
	} else if (rate < vlast) {
		vdec++;
	}
	vlast = $7;
}


/closing/ {
	if (r == 0 || s == 0) {
		printf("%s: FAILED\n", FILENAME);
	} else {
		if (r != s) {
			status = "INCOMPLETE";
		} else {
			status = "SUCCESS";
		}
		printf("%s: %-10s %d/%d segments %d bytes %1.3f seconds %1.3f mbps %d/%d rate changes\n",
			FILENAME, status, r, s, b, t, tp / r, vinc, vdec);
	}

	# update the global stats
	tb  += b;
	tt  += t;
	ttp += tp;
	tr  += r;
	tvinc += vinc;
	tvdec += vdec;

	# reset video specific stats
	b = 0;
	t = 0.0;
	tp = 0.0;
	r = 0;
	s = 0;
	vinc = 0;
	vdec = 0;
	vlast = 0;
}

/bytes/ {
	# update video specific stats
	r += 1;
	b  += $4;
	t  += $6;
	tp += $8;
}

END {
	# and finally show the cumulative stats
	print "totals";
	print tr, "segments";
	print tb / 1000000, "MB";
	print tt, "seconds";
	print ttp/ tr, "mbps";
	printf("%d/%d rate changes\n", tvinc, tvdec);
}
